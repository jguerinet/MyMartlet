/*
 * Copyright 2014-2021 Julien Guerinet
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'kotlin'

buildscript {
    ext.kotlin_version = '1.4.0'
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.29.0'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.2'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:3.17.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

subprojects {
    apply from: '../spotless.gradle'
}

// With the Dependency Updates plugin, only look for dependencies that aren't alpha/beta/rc/etc.. versions
dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'eap', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

/**
 * Generates the Changelog file from the XML file
 */
task generateChangeLogMd() {
    def parsedProjectXml = new XmlParser()
            .parse("$project.rootDir/app/src/main/res/xml/changelog.xml")
    def sw = new StringWriter()
    sw.append("# Change Log\n")
    parsedProjectXml.depthFirst().each {
        switch (it.name()) {
            case "version":
                sw.append("\n## ${it.@title}\n")
                break
            case "item":
                if (it.@text?.trim())
                    sw.append("* ${it.@text}\n")
        }
    }
    def docsDir = new File("$project.rootDir/docs")
    docsDir.mkdirs()
    def changeLogMd = new File(docsDir, "CHANGELOG.md")
    changeLogMd.createNewFile()
    changeLogMd.write(sw.toString())
}